# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.17)
{
  # A helpful description of your flake
  description = "AI Generated Website";

  # Flake inputs
  inputs = {
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*";

    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/0.1.*";

    pre-commit-hooks.url = "github:cachix/git-hooks.nix";
  };

  # Flake outputs that other flakes can use
  outputs = { self, flake-schemas, nixpkgs, pre-commit-hooks }:
    let
      # Helpers for producing system-specific outputs
      supportedSystems = [ "x86_64-linux" "aarch64-darwin" "x86_64-darwin" "aarch64-linux" ];
      forAllSystems = nixpkgs.lib.genAttrs supportedSystems;
      forEachSupportedSystem = f: nixpkgs.lib.genAttrs supportedSystems (system: f {
        pkgs = import nixpkgs { inherit system; };
      });
    in {
      # Schemas tell Nix about the structure of your flake's outputs
      schemas = flake-schemas.schemas;

      # Pre-commit hooks
      checks = forAllSystems (system: {
        pre-commit-check = pre-commit-hooks.lib.${system}.run {
          src = ./.;
          hooks = {
            no-main-branch-commits = {
              enable = true;
              entry = "bash scripts/prevent-main-commits.sh";
            };

            no-main-branch-pushes = {
              enable = true;
              entry = "bash scripts/prevent-main-push.sh";
              stages = [ "pre-push" ];
            };

            chan-ko-testing = {
              enable = true;
              name = "Chan-Ko Website Testing";
              entry = "pnpm test:chan-ko-website";
              pass_filenames = false;
              verbose = true;
            };

            end-of-file-fixer.enable = true;

            gts = {
              enable = true;
              name = "Google TypeScript Style Guide Enforcement";
              entry = "pnpm run lint:fix";
              pass_filenames = false;
            };

            trim-trailing-whitespace.enable = true;
          };
        };
      });

      # Development environments
      devShells = forEachSupportedSystem ({ pkgs }: {
        default = pkgs.mkShell {
          # Pinned packages available in the environment
          packages = with pkgs; [
            nodejs_22
            nodePackages.pnpm
            python311Packages.pre-commit-hooks
            nixpkgs-fmt
          ];

          # Environment variables
          env = {
            GREET = "AI Generated Website Repo";
          };

          # A hook run every time you enter the environment
          shellHook = ''
            echo -e "\033[0;32m### Welcome to the $GREET! ###\033[0m"
          '';
        };
      });
    };
}
